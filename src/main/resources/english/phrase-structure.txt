// shlurd:  a limited understanding of small worlds
// Copyright 2017-2018 John V. Sichi
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

preposition :=
  IN |
  TO;

SBARQ ->
  // "which dog bit me?"
  WHNP SQ |
  // "through which door did you go?"
  WHPP SQ |
  // "when did you eat?"
  WHADVP SQ;

SBAR ->
  // "[the dog] that is hungry"
  WHNP S |
  // "if the dog bites the woman"
  IN S;

WHNP ->
  // "which apple in the basket"
  WHNP PP |
  // "who"
  WP |
  // "how many red cats"
  WHADJP NP |
  // "which"
  WDT |
  // "which cat"
  WDT NP |
  // "whose"
  WP_POS |
  // "whose umbrella"
  WP_POS NP;

WHADVP ->
  // "why"
  WRB;

WHADJP ->
  // "how tall"
  WRB JJ;

WHPP ->
  // "by whose authority"
  preposition WHNP;

SQ ->
  // "go home"
  VP |
  // "must (not) the werewolf (by the light of the moon) drink blood"
  MD (ADVP) NP (ADVP) VP |
  // "is (not) the werewolf (now) (in the cage) (alive)"
  VB (ADVP) NP (ADVP) (PP) (ADJP) |
  // "is (not) the werewolf (thirstily) drinking blood"
  VB (ADVP) NP (ADVP) VP |
  // "is (not) the werewolf (really) Dan"
  VB (ADVP) NP (ADVP) NP |
  // "is (not) the werewolf (now) happy in the cage"
  VB (ADVP) NP (ADVP) ADJP PP;

plainS :=
  VP |
  NP VP |
  ADVP NP VP |
  ADVP ADVP NP VP |
  PP NP VP;

S ->
  plainS |
  SBAR COMMA plainS |
  TMOD COMMA plainS |
  TMOD plainS;

SINV ->
  ADVP VB NP VP |
  PP VP NP VP;

VP ->
  MD VP |
  MD ADVP VP |
  VB |
  VB NP |
  VB NP PP |
  VB NP NP |
  VB NP PRT |
  VB NP PRT PP |
  VB NP ADVP |
  VB NP ADVP PP |
  VB ADVP VP |
  VB ADVP NP |
  VB ADVP ADJP |
  VB ADVP PP |
  VB VP |
  VB PP |
  VB PP TMOD |
  VB PRT NP |
  VB PRT NP ADVP |
  VB ADJP |
  VB ADVP |
  VBG |
  VBG NP |
  VBG ADJP |
  ADVP VB |
  ADVP VB NP |
  ADVP VBG |
  ADVP VBG NP |
  VBN;

ADJP ->
  VB |
  DT JJ CC JJ |
  VBG |
  JJ |
  JJ CC VBN |
  JJ CC JJ |
  JJ PP |
  ADVP JJ |
  VBN |
  ADJP CC ADJP |
  ADJP COMMA ADJP COMMA CC ADJP |
  ADJP COMMA ADJP COMMA ADJP COMMA CC ADJP;

NP ->
  DT NN |
  DT NN POS |
  DT NN NN |
  DT NN NN NN |
  DT NN CC NN |
  DT JJ NN |
  DT JJ NN NN |
  DT JJ JJ NN |
  DT VBN NN |
  NP PP |
  NP PP PP |
  NP NN |
  NP NN POS |
  NP CC NP |
  NP SBAR |
  NP NP |
  NP COMMA NP |
  NP COMMA NP COMMA CC NP |
  PRP |
  NN |
  NN POS |
  NN CC NN |
  NN NN |
  NN NN POS |
  JJ NN |
  PRP_POS |
  PRP_POS NN |
  ADJP NN |
  LPAREN NP RPAREN |
  LPAREN NP RPAREN POS;

TMOD -> NP;

PP ->
  preposition |
  preposition NP;

ADVP ->
  IN |
  RB |
  RB RB |
  ADVP PP |
  RB CC RB;

PRT -> RB;
