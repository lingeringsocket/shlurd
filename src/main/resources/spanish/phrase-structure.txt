// shlurd:  a limited understanding of small worlds
// Copyright 2017-2018 John V. Sichi
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

verbmod :=
  // "sobre la mesa"
  PP |
  // "por cuál puerta"
  WHPP |
  // "rapidamente y cruelmente"
  ADVP |
  // "cuándo"
  WHADVP |
  // "ayer"
  TMOD
  ;

nounPhrase :=
  // "el perro"
  NP |
  // "qué perro"
  WHNP
  ;

preposition :=
  IN;

// Spanish does not allow dangling prepositions, so we
// don't use DPP anywhere here

plainS :=
  // "estoy triste"
  VP |
  // "(frecuentemente) ella va al mercado"
  (verbmod)* nounPhrase VP;

sentenceListElem :=
 S COMMA;

S ->
  plainS |
  //  "soy un hombre y eres una mujer"
  S CC S |
  // "soy un hombre, eres una mujer, y ellos son perros"
  (sentenceListElem)+ CC S |
  // "ayer, fueron a la playa"
  TMOD COMMA plainS |
  // "ayer fueron a la playa"
  TMOD plainS;

SBAR ->
  // "[el perro] que tiene hambre"
  WHNP S |
  // "si el perro muerde a la mujer"
  preposition S;

nounPreModifier :=
  // "rojo"
  JJ;

nounPostModifier :=
  // "rojo"
  JJ |
  // "derrotado"
  VBN;

npSimple :=
  // "(el) (grande) sueño (roto)"
  (DT) (nounPreModifier)* NN (nounPostModifier)* |
  // "yo"
  PRP |
  // "el mío"
  (DT) PRP_POS |
  // "mi cabro"
  PRP_POS NN;

npListElem :=
  nounPhrase COMMA;

NP ->
  // "el perro"
  npSimple |
  // "el perro ((un caniche))"
  NP NP |
  // "el rey de las ratas"
  NP (PP)+ |
  // "el hombre que tiene todos"
  NP SBAR |
  // "herrero grande y fuerte"
  NP ADJP |
  // "{el otro}"
  LCURLY nounPhrase RCURLY |
  // "((el otro))"
  LPAREN nounPhrase RPAREN |
  // "(ni) el zorro ni la gallina"
  (DT) NP CC NP |
  // "(ni) el zorro, la gallina ni la paloma"
  (DT) (npListElem)+ NP CC NP |
  // "(ni) el zorro, la gallina, ni la paloma"
  (DT) npListElem (npListElem)+ CC NP;

verbSimple := VB | VBG | VBN;

VP ->
  // "mata (rapidamente) (el ladrón) (con un cuchillo)"
  (verbmod)* verbSimple (verbmod)* (nounPhrase) (verbmod)* |
  // "(rapidamente) (le) da (a la ladrona) un pepinillo (con sal)"
  (verbmod)* (PRP) verbSimple (verbmod)* (nounPhrase) (verbmod)* |
  // "(rapidamente) lo le da (a la ladrona) (con sal)"
  (verbmod)* PRP PRP verbSimple (verbmod)* (nounPhrase) (verbmod)* |
  // "es (el sol) rojo" |
  (verbmod)* verbSimple (nounPhrase) ADJP (verbmod)* |
  // "es rojo el sol" |
  (verbmod)* verbSimple ADJP nounPhrase (verbmod)* |
  // "es Juan el rey"
  (verbmod)* verbSimple (verbmod)* nounPhrase (verbmod)* nounPhrase (verbmod)* |
  // "estoy deciendo"
  (verbmod)* VB VP |
  // "tiene que decier"
  (verbmod)* VB preposition VP;

adjSimple :=
  // "corriendo"
  VBG |
  // "comido"
  VBN |
  // "rojo"
  JJ |
  // "rojo de vergüenza"
  JJ PP;

adjListElem :=
  adjSimple COMMA;

ADJP ->
  // "rojo"
  adjSimple |
  // "(ni) rojo ni azul"
  (DT) adjSimple CC adjSimple |
  // "(ni) rojo, azul ni amarillo"
  (DT) (adjListElem)+ adjSimple CC adjSimple |
  // "(ni) rojo, azul, ni amarillo"
  (DT) adjListElem (adjListElem)+ CC adjSimple;

PP ->
  // "en el medio"
  preposition NP;

advSimple :=
  // "(muy) rapidamente"
  (RB)+ |
  // "rapidamente para variar"
  ADVP PP;

advListElem :=
  advSimple COMMA;

ADVP ->
  advSimple |
  advSimple CC advSimple |
  (advListElem)+ advSimple CC advSimple |
  advListElem (advListElem)+ CC advSimple;

TMOD ->
  // "ayer por la tarde"
  NP;

WHNP ->
  // "cuál manzana en la canasta"
  WHNP PP |
  // "quién"
  WP |
  // "cuántos gatos rojos"
  WHADJP NP |
  // "which"
  WDT |
  // "cuál gato"
  WDT NP |
  // "de quién"
  WP_POS |
  // "cuyo paraguas"
  WP_POS NP |
  // {cuál cabra en la granja}
  LCURLY WHNP RCURLY |
  // ((cuál cabra en la granja))
  LPAREN WHNP RPAREN;

WHADVP ->
  // "por qué"
  WRB;

WHADJP ->
  // "cuántos
  WRB |
  // "cuán alto"
  WRB JJ;

WHPP ->
  // "en que casa"
  preposition WHNP;
